WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ LINE_COMMENT | BLOCK_COMMENT }
LINE_COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* }
BLOCK_COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
NEWLINE = _{ "\r\n" | "\n" }

// Top Level

program = { SOI ~ Item* ~ EOI }
Item = { Decl | FuncDef }

// Declaration

Decl = { ConstDecl | VarDecl }

ConstDecl = { "const" ~ BType ~ ConstDef ~ ("," ~ ConstDef)* ~ ";" }
BType = { "int" | "float" }
ConstDef = { Ident ~ ( "[" ~ ConstExp ~ "]" )* ~ "=" ~ ConstInitVal }
ConstInitVal = { ConstExp | ("{" ~ (ConstInitVal ~ ( "," ~ ConstInitVal )* )? ~ "}" ) }

VarDecl = { BType ~ VarDef ~ ( "," ~ VarDef )* ~ ";" }
VarDef = { Ident ~ ( "[" ~ ConstExp ~ "]" )* ~ ( "=" ~ InitVal )? }
InitVal = { Exp | ("{" ~ (InitVal ~ ( "," ~ InitVal )* )? ~ "}" ) }

// Function

FuncDef = { FuncType ~ Ident ~ "(" ~ FuncFParams? ~ ")" ~ Block }
FuncType = { "int" | "float" | "void" }

FuncFParams = { FuncFParam ~ ( "," ~ FuncFParam )* }
FuncFParam = { BType ~ Ident ~ ( "[" ~ "]" ~ ( "[" ~ Exp ~ "]" ) )? }

// Statement

Block = { "{" ~ BlockItem* ~ "}" }
BlockItem = { Decl | Stmt }

Stmt = {
    Block 
    | "if" ~ "(" ~ Cond ~ ")" ~ Stmt ~ ( "else" ~ Stmt )?
    | "while" ~ "(" ~ Cond ~ ")" ~ Stmt
    | ( "break" | "continue" )~ ";"
    | "return" ~ Exp? ~ ";"
    | LVal ~ "=" ~ Exp ~ ";"
    | Exp? ~ ";" 
}

// Expression

Cond = { LOrExp }

LVal = { Ident ~ ( "[" ~ Exp ~ "]" )* }

LOrExp = { LAndExp ~ ( "||" ~ LAndExp )* }
LAndExp = { EqExp ~ ( "&&" ~ EqExp )* }
EqExp = { RelExp ~ ( ( "==" | "!=" ) ~ RelExp )* }
RelExp = { AddExp ~ ( ( "<" | ">" | "<=" | ">=" ) ~ AddExp )* }
AddExp = { MulExp ~ ( ( "+" | "-" ) ~ MulExp )* }
MulExp = { UnaryExp ~ ( ( "*" | "/" | "%" ) ~ UnaryExp )* }
UnaryExp = {
    PrimaryExp
  | ("+" | "-" | "!") ~ UnaryExp
}
PrimaryExp = {
    "(" ~ Exp ~ ")"
    | Imm
    | Ident ~ "(" ~ FuncRParams? ~ ")"
    | LVal
}

FuncRParams = { Exp ~ ( "," ~ Exp )* }

Exp = { AddExp }
ConstExp = { AddExp }


// Lexical

Imm = { INT_IMM | FLOAT_IMM }
INT_IMM = @{ DEC_INT | HEX_INT | OCT_INT}

DEC_INT = { "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
HEX_INT = { ("0x" | "0X") ~ ASCII_HEX_DIGIT+ }
OCT_INT = { "0" ~ ASCII_OCT_DIGIT+ }

FLOAT_IMM = @{ DEC_FLOAT | HEX_FLOAT }

DEC_FLOAT = { (DEC_FRAC_CONST | DEC_DIGIT_SEQ ) ~ DEC_EXP? }
HEX_FLOAT = { ("0x" | "0X") ~ (HEX_FRAC_CONST | HEX_DIGIT_SEQ ) ~ HEX_EXP? }

DEC_FRAC_CONST = @{ (DEC_DIGIT_SEQ? ~ "." ~ DEC_DIGIT_SEQ) | (DEC_DIGIT_SEQ ~ ".") }
DEC_EXP = @{ ("e" | "E") ~ SIGN? ~ DEC_DIGIT_SEQ }

HEX_FRAC_CONST = @{ (HEX_DIGIT_SEQ? ~ "." ~ HEX_DIGIT_SEQ) | (HEX_DIGIT_SEQ ~ ".") }
HEX_EXP = @{ ("p" | "P") ~ SIGN? ~ DEC_DIGIT_SEQ }

SIGN = { "+" | "-"}

DEC_DIGIT_SEQ = { ASCII_DIGIT+ }
HEX_DIGIT_SEQ = { ASCII_HEX_DIGIT+ }

Ident = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }